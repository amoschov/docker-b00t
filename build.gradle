buildscript {
    ext {
        springBootVersion = '1.2.3.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") 
        classpath 'com.bmuschko:gradle-docker-plugin:2.5'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse-wtp'
apply plugin: 'spring-boot'
apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'


import java.io.File
import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerTagImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStopContainer

version = '1.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = 'perk0ns-web'
    version =  '0.0.1-SNAPSHOT'
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
 compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.flywaydb:flyway-core")
    runtime("mysql:mysql-connector-java")
    testCompile("org.springframework.boot:spring-boot-starter-test")
}


docker {
    if (System.env.DOCKER_HOST) {
        url = "$System.env.DOCKER_HOST".replace("tcp","https")
        if (System.env.DOCKER_CERT_PATH) {
            certPath = new File(System.env.DOCKER_CERT_PATH)

        }
    } else {
        url = 'unix:///var/run/docker.sock'
    }
}

task copyDockerFile(type: Copy) {
    from        "$project.projectDir/src/main/docker"
    into        "$project.buildDir"
}

task buildDocker(type: DockerBuildImage) {
    dependsOn build
    dependsOn copyDockerFile
    quiet = false
    noCache = false
    println("------------" + project.buildDir )
    inputDir = project.buildDir
    tag = "dockerboot/$project.name:$project.version"
}

task tagDocker(type: DockerTagImage) {
    dependsOn buildDocker
    force = true
    imageId = buildDocker.getTag()
    tag = "$project.version"
}

//task pushDocker(type: DockerPushImage ) {
//    dependsOn tagDocker
//    imageName = "$registry.url/$project.name"
//}

task createDocker(type: DockerCreateContainer) {
    dependsOn buildDocker
    imageId = buildDocker.getTag()
}

task runDocker(type: DockerStartContainer) {
    dependsOn createDocker
    targetContainerId { createDocker.getContainerId() }
}

task stopDocker(type: DockerStopContainer) {
    dependsOn createDocker
    targetContainerId { createDocker.getContainerId() }
}



//task buildDocker(type: Docker, dependsOn: build) {
//  push = false
//  dockerfile = file('src/main/docker/Dockerfile')
//  applicationName = jar.baseName
//  doFirst {
//    copy {
//      from jar
//      into stageDir
//    }
//  }
// }