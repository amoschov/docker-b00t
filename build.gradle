buildscript {
    ext {
        springBootVersion = '1.2.5.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.bmuschko:gradle-docker-plugin:2.5'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerTagImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.DockerListImages
import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStopContainer

version = '1.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = 'perk0ns-web'
    version =  '0.0.1-SNAPSHOT'
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
 compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.flywaydb:flyway-core")
    runtime("mysql:mysql-connector-java")
    testCompile("org.springframework.boot:spring-boot-starter-test")
}


docker {
    if (System.env.DOCKER_HOST) {
        url = "$System.env.DOCKER_HOST".replace("tcp","https")
        if (System.env.DOCKER_CERT_PATH) {
            certPath = new java.io.File(System.env.DOCKER_CERT_PATH)
        }
    } else {
        url = 'unix:///var/run/docker.sock'
    }
}

task prepareDockerResources(type: Copy) {
    from jar
    from "$project.projectDir/src/main/docker/Dockerfile"
    into "$project.buildDir/docker"
}

// docker tag [OPTIONS] IMAGE[:TAG] [REGISTRYHOST/][USERNAME/]NAME[:TAG]

def repName="localhost:5000/$project.name"
def tagName="$repName:$project.version"

task buildDocker(type: DockerBuildImage) {
    dependsOn build
    dependsOn prepareDockerResources
    quiet = false
    noCache = true
    inputDir = prepareDockerResources.destinationDir
    tag = tagName
}

task tagDocker(type: DockerTagImage) {
    dependsOn buildDocker
    force = true
    imageId = buildDocker.getTag()
    repository = repName
    tag = "latest"
}

task pushDocker(type: DockerPushImage ) {
    dependsOn tagDocker
    imageName = repName
    // optional
    tag = project.version
}

task createDocker(type: DockerCreateContainer) {
    dependsOn buildDocker
    imageId = buildDocker.getTag()
    links = ['perkonsmysql:localhost']
    exposedPorts =["tcp":8080]
    portBindings = '8080:8080'
    containerName = "$project.name-$project.version"
}

task startDocker(type: DockerStartContainer) {
    dependsOn createDocker
    targetContainerId { createDocker.getContainerId() }
}

task stopDocker(type: DockerStopContainer) {
    dependsOn createDocker
    targetContainerId { createDocker.getContainerId() }
}

task startMySQLDocker(type: DockerStartContainer) {
//TODO: still not ready
}

task stopMySQLDocker(type: DockerStopContainer) {
//TODO: still not ready
}

task getMySQLId(type: DockerListImages) {

}
